#ifndef __AVGPOOL_LAYER_KERNELS_CL__
#define __AVGPOOL_LAYER_KERNELS_CL__

static const char* const avgpool_layer_kernel_source = CONVERT_KERNEL_TO_STRING(

__kernel void forward_avgpool_layer_kernel(int n, int w, int h, int c, __global float *input, __global float *output)
{
    int id = (get_group_id(0) + get_group_id(1)*get_num_groups(0)) * get_local_size(0) + get_local_id(0);
    if(id >= n) return;

    int k = id % c;
    id /= c;
    int b = id;

    int i;
    int out_index = (k + c*b);
    output[out_index] = 0;
    for(i = 0; i < w*h; ++i){
        int in_index = i + h*w*(k + b*c);
        output[out_index] += input[in_index];
    }
    output[out_index] /= w*h;
}

__kernel void backward_avgpool_layer_kernel(int n, int w, int h, int c, __global float *in_delta, __global float *out_delta)
{
    int id = (get_group_id(0) + get_group_id(1)*get_num_groups(0)) * get_local_size(0) + get_local_id(0);
    if(id >= n) return;

    int k = id % c;
    id /= c;
    int b = id;

    int i;
    int out_index = (k + c*b);
    for(i = 0; i < w*h; ++i){
        int in_index = i + h*w*(k + b*c);
        in_delta[in_index] += out_delta[out_index] / (w*h);
    }
}
);

#endif